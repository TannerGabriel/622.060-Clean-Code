plugins {
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "4.4.1.3373"
}

group = 'at.aau'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    dependsOn 'test'

    reports {
        xml.required = true
        xml.outputLocation = file("${buildDir}/reports/jacoco/xml/jacocoTestReport.xml")
        html.required = true
        html.outputLocation = file("${buildDir}/reports/jacoco/html")
    }

    afterEvaluate {
        def fileFilter = ['**/*Test*.class', '**/*Test$*.class', '**/Test*/*.class']
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: fileFilter)
        }))
        sourceDirectories.setFrom(files("${projectDir}/src/main/java"))
        executionData.setFrom(fileTree(dir: "${buildDir}/jacoco", includes: ["test.exec"]))
    }
}

sonar {
    properties {
        property "sonar.projectKey", "TannerGabriel_622.060-Clean-Code"
        property "sonar.organization", "tannergabriel"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.projectDir}/build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'org.jsoup:jsoup:1.17.2'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
}
